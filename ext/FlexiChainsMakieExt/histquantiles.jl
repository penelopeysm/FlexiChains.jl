rep2(vals) = repeat(vals, inner = 2)

function _plot_histquantiles!(
        ax::Makie.Axis,
        stacked_data;  # niters x nchains x nparams
        observed = nothing,
        nbins::Integer = 25,
        quantiles = FC.PlotUtils.DEFAULT_QUANTILE_LEVELS,
        color = Makie.Cycled(1),
        kwargs...,
    )
    isodd(length(quantiles)) || throw(ArgumentError("`quantiles` must have odd length"))
    edges = FC.PlotUtils.get_bin_edges(stacked_data, nbins)
    # counts is iter × chain × nbins
    counts = FC.PlotUtils.bin_count_matrices(stacked_data, edges)

    nq = length(quantiles)
    n_bands = div(nq, 2)
    median_idx = div(nq + 1, 2)
    qs = Matrix{Float64}(undef, nq, nbins)

    for b in 1:nbins
        qs[:, b] = FC.PlotUtils.compute_quantile_bands(view(counts, :, :, b), quantiles)
    end

    xs = Float64[]
    for b in 1:(length(edges) - 1)
        push!(xs, edges[b], edges[b + 1])
    end

    for i in 1:n_bands
        Makie.band!(
            ax,
            xs,
            rep2(qs[i, :]),
            rep2(qs[nq + 1 - i, :]);
            alpha = _band_alpha(i, n_bands),
            color = color,
            kwargs...,
        )
    end

    p = Makie.lines!(ax, xs, rep2(qs[median_idx, :]); color = color, linewidth = 2)

    if observed !== nothing
        obs = FC.PlotUtils.histogram_counts(observed, edges)
        p = Makie.lines!(ax, xs, rep2(Float64.(obs)); color = :black, linewidth = 2)
    end

    return Makie.AxisPlot(ax, p)
end

"""
    FlexiChains.Makie.histquantiles(chn, param_or_params; observed=nothing, nbins=25, kwargs...)

Posterior predictive check via histograms. For each posterior draw, the predictive values
are binned into a histogram; the resulting per-bin count distributions are summarised as
nested quantile ribbons. Overlaying `observed` data shows whether the model's predictive
distribution is consistent with the observations.

This function is a port of [Michael Betancourt's
`plot_hist_quantiles`](https://github.com/betanalpha/mcmc_visualization_tools).

# Keyword arguments
- `observed`: vector of observed values; its histogram (same bins) is overlaid as a line.
- `nbins`: number of equal-width bins. Defaults to `25`.
- `quantiles`: odd-length vector of levels in 0–1. Defaults to `[0.1, 0.2, ..., 0.9]`.
- `figure`, `axis`: `NamedTuple`s forwarded to `Makie.Figure` / `Makie.Axis`.
"""
function FC.Makie.histquantiles(
        chn::FC.FlexiChain,
        param;
        figure = (;),
        axis = (;),
        kwargs...,
    )
    _, _, fig = setup_figure_and_layout(1, 1, nothing, figure)
    ax = Makie.Axis(fig[1, 1]; xlabel = "value", ylabel = "counts", axis...)
    _, p = FC.Makie.histquantiles!(ax, chn, param; kwargs...)
    return Makie.FigureAxisPlot(fig, ax, p)
end

function FC.Makie.histquantiles!(ax::Makie.Axis, chn::FC.FlexiChain, param; kwargs...)
    sub = FC.PlotUtils.subset_and_split_chain(chn, param)
    ks = collect(keys(sub))
    isempty(ks) && throw(ArgumentError("no parameters to plot"))
    data = map(ks) do k
        d = FC.PlotUtils._get_raw_data(sub, k)
        FC.PlotUtils.check_eltype_is_real(d)
        d
    end
    stacked_data = stack(data) # niters × nchains × nparams
    return _plot_histquantiles!(ax, stacked_data; kwargs...)
end

function FC.Makie.histquantiles!(chn::FC.FlexiChain, param; kwargs...)
    return FC.Makie.histquantiles!(Makie.current_axis(), chn, param; kwargs...)
end
