module FlexiChainsPigeonsDynamicPPLExt

using Pigeons: Pigeons
using DynamicPPL: LogDensityFunction, UnlinkAll, ParamsWithStats, Model

using FlexiChains: FlexiChains
using AbstractMCMC

function FlexiChains._internal_from_pigeons(pt::Pigeons.PT, model::Model)
    # This is niter x nparams x nchains -- but the last param is actually the log density so
    # we drop it.
    sarr = FlexiChains._faithful_sample_array(pt)[:, 1:(end-1), :]
    # Note that Pigeons unfortunately returns us a **flattened vector** of **unlinked**
    # parameters, which is annoying. Because it's flattened, we don't have any way to
    # extract the original parameter structure, so we need to feed it back through a
    # LogDensityFunction.
    #
    # It's MOSTLY safe to use a LDF because Pigeons only handles models with fixed sizes
    # (this is stated in the Pigeons docs). Unexpected stuff WILL happen if you have a model
    # where the parameters have variable ordering, but in such a case things will get very
    # weird on the Pigeons end already, and there's nothing we can do about it here.
    # Because the parameters are unlinked we use UnlinkAll() here.
    ldf = LogDensityFunction(model, UnlinkAll())
    # Then the usual stuff, reevaluate and make a chain.
    pwss = [ParamsWithStats(s, ldf) for s in eachslice(sarr, dims=(1, 3))]
    chn = AbstractMCMC.from_samples(FlexiChains.VNChain, pwss)
end

end # module
